{
  parserClass               = "consulo.language.ignore.impl.internal.parser.IgnoreParser"
  extends                   = "consulo.language.ignore.impl.internal.psi.IgnoreElementImpl"
  version                   = "3"
 
  psiClassPrefix            = "Ignore"
  psiImplClassSuffix        = "Impl"
  psiPackage                = "consulo.language.ignore.psi"
  psiImplPackage            = "consulo.language.ignore.impl.internal.psi"

  elementTypeHolderClass    = "consulo.language.ignore.psi.IgnoreTypes"
  elementTypeClass          = "consulo.language.ignore.psi.IgnoreElementType"
  tokenTypeClass            = "consulo.language.ignore.psi.IgnoreTokenType"
 
  tokens = [
    CRLF                    = "regexp:[\s\r\n]+"
    HEADER                  = "regexp:###.*"
    SECTION                 = "regexp:##.*"
    COMMENT                 = "regexp:#.*"
    BRACKET_LEFT            = "regexp:(?!\\\[)\["
    BRACKET_RIGHT           = "regexp:(?!\\\])\]"
    SLASH                   = "/"
    SYNTAX_KEY              = "syntax:"
    VALUE                   = "regexp:(?![!#\s])(?![\[\]])(?:(?:\\[\[\]])|(?:\/\/)|[^\[\]\/\s])+"
  ]

  name("ENTRY.*")           = "entry"
  mixin("ENTRY")            = "consulo.language.ignore.impl.internal.psi.IgnoreEntryExtImpl"
  implements("ENTRY")       = "consulo.language.ignore.psi.IgnoreEntryBase"
}
 
gitignoreFile           ::= item_ *
private item_           ::= HEADER | SECTION | COMMENT | SYNTAX | ENTRY_DIRECTORY | ENTRY_FILE | CRLF
NEGATION                ::= "!"
SYNTAX                  ::= SYNTAX_KEY CRLF * VALUE

ENTRY_FILE              ::= NEGATION ? SLASH ? <<list_macro value_>>        { extends = "ENTRY" }
ENTRY_DIRECTORY         ::= NEGATION ? SLASH ? <<list_macro value_>> SLASH  { extends = "ENTRY_FILE" }
ENTRY                   ::= NEGATION ? SLASH ? <<list_macro value_>>

private meta bvalue_    ::= BRACKET_LEFT ( VALUE SLASH ? ) + BRACKET_RIGHT  { pin = "BRACKET_LEFT" }
private meta value_     ::= bvalue_ | VALUE
private meta list_macro ::= <<p>> + (SLASH <<p>> +) *